plugins {
    id('application')
}

mainClassName = 'net.rubygrapefruit.platform.test.Main'
applicationName = 'native-platform-test'
archivesBaseName = 'native-platform-test'

if (versions.useRepo) {
    configurations.all {
        resolutionStrategy.dependencySubstitution {
            substitute project(':native-platform') with module("net.rubygrapefruit:native-platform:${version}")
        }
    }
    configurations {
        download
    }
    dependencies {
        download "$group:${archivesBaseName}:${version}"
    }
}

dependencies {
    implementation project(':native-platform')
    implementation 'net.sf.jopt-simple:jopt-simple:4.2'
}

task sourceZip(type: Zip) {
    from sourceSets.main.allSource
    archiveClassifier = 'sources'
    archiveExtension = 'jar'
}

publishing {
    publications {
        main(MavenPublication) {
            groupId = project.group
            artifactId = project.archivesBaseName
            version = project.version
            artifact(distZip)
            artifact(sourceZip)
        }
    }
}

// Download and unpack the application to allow it to be tested
task download {
    doLast {
        copy {
            from(configurations.download.files.collect { zipTree(it) })
            into("$buildDir/download")
        }
    }
}

// Use System.in as input stream for the launched process to enable interactive input
tasks.named("run", JavaExec) {
    standardInput = System.in
}
